create or replace package CUSTOM_CLOUD_AI is

-- Regist public LLM AI model endpoint
    procedure  REGIST_MODEL (
        PROVIDER varchar2,
        ENDPOINT varchar2,
        AUTH varchar2,
        RESPONSE_PARSE_PATH varchar2,
        REQUEST_TEMPLATE varchar2
    );

-- Unregist public LLM AI model endpoint
    procedure UNREGIST_MODEL (
        PROVIDER varchar2
    );

-- Create profile to store model config information
    procedure CREATE_PROFILE (
        PROFILE_NAME varchar2,
        DESCRIPTION varchar2,
        ATTRIBUTES CLOB
    );

-- Delete profile to remove model config information
    procedure DROP_PROFILE (
        PROFILE_NAME varchar2
    );

-- Update prompt template in profile
    procedure UPDATE_PROFILE_PROMPT (
        PROFILE_NAME varchar2,
        TEMPLATE_NAME varchar2,
        TEMPLATE_VALUE varchar2
    );

-- Return text response generated by LLM
    function CHAT(
        PROMPT in varchar2,
        PROFILE_NAME in varchar2
    ) return varchar2;

-- Get table DDL information
    function GET_TABLE_INFO(
        SCHEMA_NAME in varchar2,
        MY_TABLE_NAME in varchar2,
        PROFILE_NAME in varchar2
        ) return varchar2;

-- Return prompt generated for SELECT AI 
    function SHOWPROMPT(
        PROMPT in varchar2,
        PROFILE_NAME in varchar2
        ) return varchar2;

-- Return SQL generated by LLM
    function SHOWSQL(
        PROMPT in varchar2,
        PROFILE_NAME in varchar2
    ) return varchar2;



end CUSTOM_CLOUD_AI;
/




create or replace package body CUSTOM_CLOUD_AI is

-- 注册模型
    procedure  REGIST_MODEL (
        PROVIDER varchar2,
        ENDPOINT varchar2,
        AUTH varchar2,
        RESPONSE_PARSE_PATH varchar2,
        REQUEST_TEMPLATE varchar2
    )
    is
    begin
        insert into CUSTOM_CLOUD_AI_REGEST_MODELS (
            MODEL_PROVIDER,
            MODEL_ENDPOINT,
            MODEL_AUTH,
            MODEL_RESPONSE_PARSE_PATH,
            MODEL_REQUEST_TEMPLATE
        ) values (
            PROVIDER,
            ENDPOINT,
            AUTH,
            RESPONSE_PARSE_PATH,
            REQUEST_TEMPLATE
        );

        commit;

        DBMS_OUTPUT.PUT_LINE('MODEL REGISTERED SUCCESSFULLY');
    exception
    /*
        WHEN DUP_VAON_INDEX THEN
            ROLLBACK;
            DBMS_OUTPUT.PUT_LINE('Error: Provider already exists');*/
        when others then
            rollback;
            DBMS_OUTPUT.PUT_LINE('ERROR REGISTERING MODEL: ' || SQLERRM);
    end REGIST_MODEL;

-- 删除模型注册信息
    procedure UNREGIST_MODEL (
        PROVIDER varchar2
    )
    is
    begin
        delete from CUSTOM_CLOUD_AI_REGEST_MODELS where MODEL_PROVIDER = PROVIDER;
        commit;
        DBMS_OUTPUT.PUT_LINE('MODEL UNREGISTERED SUCCESSFULLY');
    exception
        when NO_DATA_FOUND then
            DBMS_OUTPUT.PUT_LINE('ERROR: PROVIDER NOT FOUND');
        when others then
            rollback;
            DBMS_OUTPUT.PUT_LINE('ERROR UNREGISTERING MODEL: ' || SQLERRM);
    end UNREGIST_MODEL;

-- 创建profile
    procedure CREATE_PROFILE (
        PROFILE_NAME varchar2,
        DESCRIPTION varchar2,
        ATTRIBUTES CLOB
    )
    is
        l_json_attributes JSON_OBJECT_T;

        l_provider varchar2(256);
        l_model varchar2(256);
        l_tempreture number;
        l_max_tokens number;
        l_stop_tokens varchar2(1000);
        l_object_list JSON_ARRAY_T;
        l_object_list_clob clob;

        l_prompt_template clob := 'You are a data analyst who is proficient in Oracle SQL.

Task Description: Generate Oracle SQL queries based on the provided table schema (DDL).

Input:
1. User Question: [Specify the question the user asks, which requires a SQL query response]
2. Table Namesand Table DDL: [Provide the Data Definition Language (DDL) for the table, including column names, data types, and comments. Column comments should be used as alias in output SQL.]
3. T_EXPENSE_WIDE_TABLE is the main table, containing the expense record of persons . If the expense content includes meals, it may be associated with V_EXPENSE_BILL_DISH, which contains meal-related dish information.

Output:
SQL Query: [Generated SQL query based on the given task]

Additional Instructions/Notes:
1. Ensure that the generated SQL query is syntactically correct and applicable to the provided table schema.
2. Only generate SELECT SQL queries, never answer INSERT, UPDATE, DELETE etc.
3. If applicable, provide additional context or constraints that should be considered when generating the SQL query.
4. Generate plain text without markdown formate. Do not write anything else except the SQL query.
5. Always use English column names and Chinese column alias. 
6. Select proper columns. Keep the column sort order as above.
7. If the question is too cpmplex to answer, you can think it step by step.

Input:
QUESTION:<prompt>

Oracle databse tables with their properties:
<table_infos>

<Example SQL pitch>

Output:';
    
    l_prompt_ddl clob := '### Table meaning: <table_comment>
column_name data_type, 
CREATE TABLE  <schema>.<table_name> (
<column_info>';

    begin
        -- 解析attributes JSON 数据
        l_json_attributes := JSON_OBJECT_T(ATTRIBUTES);

        l_provider := NVL(
                NVL( l_json_attributes.get_String('provider'),l_json_attributes.get_String('PROVIDER')
            ), NULL);
        l_model := NVL(
                NVL( l_json_attributes.get_String('model'),l_json_attributes.get_String('MODEL')
            ), NULL);
        l_tempreture := NVL(
                NVL( l_json_attributes.get_Number('tempreture'),l_json_attributes.get_Number('TEMPRETURE')
            ), 0);
        l_max_tokens := NVL(
                NVL( l_json_attributes.get_Number('max_tokens'),l_json_attributes.get_Number('MAX_TOKENS')
            ), NULL);
        l_stop_tokens := NVL(
                NVL( l_json_attributes.get_String('stop_tokens'),l_json_attributes.get_String('STOP_TOKENS')
            ), '[]');

        l_object_list := NVL(
                NVL( l_json_attributes.get_Array('object_list'),l_json_attributes.get_Array('OBJECT_LIST')
            ), NULL);

        IF l_object_list IS NOT NULL THEN l_object_list_clob := l_object_list.to_clob; END IF;        

        IF l_provider IS NULL OR l_model IS NULL OR l_object_list IS NULL THEN
            DBMS_OUTPUT.PUT_LINE('ERROR: PROFILE INFORMATION IS NOT COMPLETE!!');        
        END IF;
        
        -- 插入数据到表CUSTOM_CLOUD_AI_PROFILES
        insert into CUSTOM_CLOUD_AI_PROFILES (
                    AI_PROFILE_NAME, AI_DESCRIPTION, ATTRIBUTES, PROVIDER, MODEL, TEMPERATURE, MAX_TOKENS, STOP_TOKENS, OBJECT_LIST, PROMPT_TEMPLATE, PROMPT_DDL
                ) values (
                    PROFILE_NAME, DESCRIPTION, ATTRIBUTES, l_provider, l_model, l_tempreture, l_max_tokens, l_stop_tokens, l_object_list_clob, l_prompt_template, l_prompt_ddl
                );

        commit;

        DBMS_OUTPUT.PUT_LINE('PROFILE CREATED SUCCESSFULLY');
        
    exception
        when others then
            rollback;
            DBMS_OUTPUT.PUT_LINE('ERROR CREATING PROFILE: ' || SQLERRM);
    end CREATE_PROFILE;

-- 删除profile
    procedure DROP_PROFILE (
        PROFILE_NAME varchar2
    )
    is
    begin
        delete from CUSTOM_CLOUD_AI_PROFILES where AI_PROFILE_NAME = PROFILE_NAME;
        commit;
        DBMS_OUTPUT.PUT_LINE('PROFILE DROPPED SUCCESSFULLY');
    exception
        when NO_DATA_FOUND then
            DBMS_OUTPUT.PUT_LINE('ERROR: PROFILE NOT FOUND');
        when others then
            rollback;
            DBMS_OUTPUT.PUT_LINE('ERROR DROPPING PROFILE: ' || SQLERRM);
    end DROP_PROFILE;

-- Update prompt in profile
    procedure UPDATE_PROFILE_PROMPT (
        PROFILE_NAME varchar2,
        TEMPLATE_NAME varchar2,
        TEMPLATE_VALUE varchar2
    )
    is
    begin
        if UPPER(TEMPLATE_NAME) = 'PROMPT_TEMPLATE' then
            update CUSTOM_CLOUD_AI_PROFILES
            set PROMPT_TEMPLATE = TEMPLATE_VALUE
            where AI_PROFILE_NAME = PROFILE_NAME;
        elsif UPPER(TEMPLATE_NAME) = 'PROMPT_DDL' then
            update CUSTOM_CLOUD_AI_PROFILES
            set PROMPT_DDL = TEMPLATE_VALUE
            where AI_PROFILE_NAME = PROFILE_NAME;
        else
            DBMS_OUTPUT.PUT_LINE('INVALID TEMPLATE NAME');
            RETURN;
        end if;

        commit;
        DBMS_OUTPUT.PUT_LINE('PROFILE PROMPT UPDATED SUCCESSFULLY');
    exception
        when NO_DATA_FOUND then
            DBMS_OUTPUT.PUT_LINE('ERROR: PROFILE NOT FOUND');
        when others then
            rollback;
            DBMS_OUTPUT.PUT_LINE('ERROR UPDATING PROFILE PROMPT: ' || SQLERRM);
    end UPDATE_PROFILE_PROMPT;

-- Return text response generated by LLM
    function CHAT (
        PROMPT varchar2,
        PROFILE_NAME varchar2
    ) return varchar2
    is        
        l_request_body  varchar2(32767);

        L_HTTP_REQUEST  UTL_HTTP.REQ;
        L_HTTP_RESPONSE UTL_HTTP.RESP;

        L_PROVIDER      varchar2(256);
        L_URL           varchar2(4000);
        L_TOKEN         varchar2(4000);
        L_RESPONSE_BODY clob;
        L_JSONPATH      varchar2(4000);
        L_ANSWER        varchar2(4000);

        V_PROMPT        varchar2(4000);
        V_MODEL         varchar2(256);
        V_TEMPERATURE   number;
        V_MAX_TOKENS    number;
        V_STOP_TOKENS   varchar2(256);
    begin
        -- Retrieve profile information
        select PROVIDER, MODEL, TEMPERATURE, MAX_TOKENS, STOP_TOKENS
        into L_PROVIDER, V_MODEL, V_TEMPERATURE, V_MAX_TOKENS, V_STOP_TOKENS
        from CUSTOM_CLOUD_AI_PROFILES 
        where UPPER(AI_PROFILE_NAME) = UPPER(PROFILE_NAME);

        -- Retrieve model information
        select MODEL_PROVIDER, MODEL_ENDPOINT, MODEL_AUTH, MODEL_RESPONSE_PARSE_PATH, MODEL_REQUEST_TEMPLATE
        into L_PROVIDER, L_URL ,L_TOKEN, L_JSONPATH, l_request_body
        from CUSTOM_CLOUD_AI_REGEST_MODELS
        where UPPER(MODEL_PROVIDER) = UPPER(L_PROVIDER);

        V_PROMPT := REPLACE(REPLACE(PROMPT,CHR(13),'\n'),CHR(10),'\n');
        V_PROMPT := REPLACE(V_PROMPT,'"','\"');

        -- Construct request body
        l_request_body := REPLACE(l_request_body,'<CONTENT>',V_PROMPT);
        l_request_body := REPLACE(l_request_body,'<MODEL>',V_MODEL);
        l_request_body := REPLACE(l_request_body,'<TEMPERATURE>',TO_CHAR(V_TEMPERATURE));
        l_request_body := REPLACE(l_request_body,'<MAX_TOKENS>',NVL(TO_CHAR(V_MAX_TOKENS),'null'));
        l_request_body := REPLACE(l_request_body,'<STOP>',V_STOP_TOKENS);
        
         -- Open HTTP request
        L_HTTP_REQUEST := UTL_HTTP.BEGIN_REQUEST(URL => L_URL, METHOD => 'POST', HTTP_VERSION => 'HTTP/1.1');        
          
        UTL_HTTP.set_header(l_http_request, 'Content-Type', 'application/json');
        UTL_HTTP.set_header(l_http_request, 'Accept', 'application/json');
        UTL_HTTP.set_header(l_http_request, 'Authorization', 'Bearer ' || l_token);

        UTL_HTTP.SET_BODY_CHARSET(L_HTTP_REQUEST, 'UTF-8');
        UTL_HTTP.SET_HEADER(L_HTTP_REQUEST, 'Content-Length', LENGTHB(l_request_body));       
        
        UTL_HTTP.WRITE_TEXT(L_HTTP_REQUEST, l_request_body);
        DBMS_OUTPUT.put_line(l_request_body);

        -- Get HTTP response
        L_HTTP_RESPONSE := UTL_HTTP.GET_RESPONSE(L_HTTP_REQUEST);
        
        -- Read response body
        UTL_HTTP.READ_TEXT(L_HTTP_RESPONSE, L_RESPONSE_BODY);
        
        -- Close HTTP response
        UTL_HTTP.END_RESPONSE(L_HTTP_RESPONSE);
        L_RESPONSE_BODY := REPLACE(L_RESPONSE_BODY, '''', '''''');
        
        -- Handle response
        DBMS_OUTPUT.put_line('Response: ' || l_response_body);
        -- DBMS_OUTPUT.put_line('SELECT JSON_VALUE('''||L_RESPONSE_BODY||''','''||L_JSONPATH||''') FROM DUAL');
        execute immediate 'SELECT JSON_VALUE('''||L_RESPONSE_BODY||''','''||L_JSONPATH||''') FROM DUAL' into L_ANSWER;
        
        L_ANSWER := trim(L_ANSWER);
        
        if SUBSTR(L_ANSWER, -1) = ';' then
            L_ANSWER := SUBSTR(L_ANSWER, 1, LENGTH(L_ANSWER) - 1);
        end if;

        if L_ANSWER is not null then
            return L_ANSWER;
        else
            return L_RESPONSE_BODY;
        end if;
    exception
        when UTL_HTTP.TOO_MANY_REQUESTS then
            return 'HTTP 429 Too Many Requests';
        when UTL_HTTP.REQUEST_FAILED then
            return 'HTTP request failed';
        when others then
            return 'An error occurred: ' || SQLERRM;
    end CHAT;

 -- Get table DDL information
    function GET_TABLE_INFO(
        SCHEMA_NAME in varchar2,
        MY_TABLE_NAME in varchar2,
        PROFILE_NAME in varchar2
        ) return varchar2 is
        -- TEMPLATE_TABLE varchar(32767);
        TABLE_INFO varchar(32767);
        TABLE_COMMENT varchar(32767);
        COLUMN_INFO varchar(32767);
        begin
            select PROMPT_DDL into TABLE_INFO 
            from CUSTOM_CLOUD_AI_PROFILES 
            where UPPER(AI_PROFILE_NAME)=UPPER(PROFILE_NAME);
                
            -- TABLE_INFO :=  REPLACE(TEMPLATE_TABLE,chr(10),'\n');

            select COMMENTS into TABLE_COMMENT
            from ALL_TAB_COMMENTS
            where OWNER=SCHEMA_NAME and TABLE_NAME = MY_TABLE_NAME and COMMENTS is not null;

            TABLE_INFO := REPLACE(TABLE_INFO,'<schema>',SCHEMA_NAME);
            TABLE_INFO := REPLACE(TABLE_INFO,'<table_name>',MY_TABLE_NAME);
            TABLE_INFO := REPLACE(TABLE_INFO,'<table_comment>',TABLE_COMMENT);

            select listagg(COL_INFO,chr(10)) into COLUMN_INFO
            from (
                select COLUMN_NAME||' '||DATA_TYPE||','||COMMENTS as COL_INFO
                from (
                    select A.COLUMN_NAME,A.DATA_TYPE,
                        case when B.COMMENTS is not null then ' -- '||B.COMMENTS else '' end as COMMENTS
                    from ALL_TAB_COLUMNS A
                    left join ALL_COL_COMMENTS B on A.OWNER=B.OWNER and A.TABLE_NAME=B.TABLE_NAME and A.COLUMN_NAME=B.COLUMN_NAME
                    where A.OWNER=SCHEMA_NAME and A.TABLE_NAME= MY_TABLE_NAME
                    order by COLUMN_ID asc
                )
            );
            
            TABLE_INFO := REPLACE(TABLE_INFO,'<column_info>',COLUMN_INFO);
            TABLE_INFO := TABLE_INFO||CHR(10)||');'||CHR(10);
            -- EXCEPTION WHEN NO_DATA_FOUND THEN NULL; 
            return TABLE_INFO;
        end GET_TABLE_INFO;

-- Return prompt generated for SELECT AI 
    function SHOWPROMPT(
        PROMPT in varchar2,
        PROFILE_NAME in varchar2
        ) return varchar2 is
        l_object_list clob;
        l_object_list_array JSON_ARRAY_T;
        l_object_elem JSON_ELEMENT_T;
        l_object JSON_OBJECT_T;
        l_owner VARCHAR2(1024);
        l_table_name VARCHAR2(1024);
        TABLE_INFOS varchar(32767) := '';
        EXAMPLE_SQL varchar2(32767);
        PROMPT_NEW varchar2(32767);
        TEMPLATE_POMPT varchar2(32767);
        begin
            select PROMPT_TEMPLATE into TEMPLATE_POMPT 
            from CUSTOM_CLOUD_AI_PROFILES 
            where UPPER(AI_PROFILE_NAME)=UPPER(PROFILE_NAME);
            
            SELECT OBJECT_LIST INTO l_object_list
            from CUSTOM_CLOUD_AI_PROFILES
            where UPPER(AI_PROFILE_NAME)=UPPER(PROFILE_NAME);

            l_object_list_array := JSON_ARRAY_T(l_object_list);

            FOR indx IN 0 .. l_object_list_array.get_size - 1
            LOOP
              l_object_elem := l_object_list_array.get(indx);
              l_object := TREAT (l_object_elem AS JSON_OBJECT_T);
              l_owner := NVL(l_object.get_string('owner'),l_object.get_string('OWNER'));
              l_table_name := NVL(l_object.get_string('name'),l_object.get_string('NAME'));
              begin
                        TABLE_INFOS := TABLE_INFOS || GET_TABLE_INFO(l_owner,l_table_name,PROFILE_NAME);
                    exception
                        when NO_DATA_FOUND then
                            -- 如果没有找到数据，则跳过当前循环迭代
                            continue;
                    end;
            END LOOP;            

            -- example_sql := match_keyword(PROMPT, MY_PROFILE_NAME);
            
            PROMPT_NEW := REPLACE(TEMPLATE_POMPT,'<table_infos>',TABLE_INFOS);
            
            prompt_new := REPLACE(prompt_new,'<Example SQL pitch>','');
            PROMPT_NEW := REPLACE(PROMPT_NEW,'<prompt>',PROMPT);

            -- PROMPT_NEW := REPLACE(PROMPT_NEW,chr(10),'\n');

            return PROMPT_NEW;
        end SHOWPROMPT;

-- Return SQL generated by LLM
  function SHOWSQL (
        PROMPT       in varchar2,
        PROFILE_NAME in varchar2
        ) return varchar2 is
            OUTPUT_RESPONSE varchar(32767);
            PROMPT_TEXT     varchar(32767);
            COMMENTS_ALL varchar(32767);
            CUSTOM_PROMPT varchar(32767);
            EXAMPLE_SQL varchar(32767) := '';
        begin
            
            CUSTOM_PROMPT := SHOWPROMPT(PROMPT,PROFILE_NAME);
            select
                CHAT(
                    PROMPT => CUSTOM_PROMPT,
                    PROFILE_NAME => PROFILE_NAME)
            into OUTPUT_RESPONSE
            from DUAL; 
            return OUTPUT_RESPONSE;

    end SHOWSQL;


end CUSTOM_CLOUD_AI;
/